mdp

// TEAM 1
	// Knight
const double A_acc = 0.75;
const int A_hea = 8;
const int A_dmg = 3;
	// Archer
const double B_acc = 0.75;
const int B_hea = 7;
const int B_dmg = 2;

// TEAM 2
	// Knight
const double C_acc = 0.75;
const int C_hea = 8;
const int C_dmg = 3;
	// Archer
const double D_acc = 0.75;
const int D_hea = 7;
const int D_dmg = 2;

module game
	a_hea : [-2..A_hea];
	b_hea : [-2..B_hea];
	c_hea : [-2..C_hea];
	d_hea : [-2..D_hea];
	turn_clock : [0..2];
	attack : [0..7];			// Chosen action:
	// 0 : NONE, 1 : A_C, 2 : A_D, 3 : B_opp, 4 : C_A, 5 : C_B, 6 : D_opp, 7 : NEXT_TURN.
	a_stun : bool;
	b_stun : bool;
	c_stun : bool;
	d_stun : bool;

	[flip_coin]	turn_clock = 0 ->
				0.5 : (turn_clock' = 1) + 0.5 : (turn_clock' = 2);

	[team_1_turn] turn_clock = 1 & attack = 0 & a_hea > 0 & b_hea > 0 & c_hea > 0 & d_hea > 0 ->
				1/3 : (attack' = 1) +
				1/3 : (attack' = 2) +
				1/3 : (attack' = 3) ;
	[team_1_turn] turn_clock = 1 & attack = 0 & a_hea <= 0 & b_hea > 0 & c_hea > 0 & d_hea > 0 ->
				1/1 : (attack' = 3) ;
	[team_1_turn] turn_clock = 1 & attack = 0 & a_hea > 0 & b_hea <= 0 & c_hea > 0 & d_hea > 0 ->
				1/2 : (attack' = 1) +
				1/2 : (attack' = 2) ;
	[team_1_turn] turn_clock = 1 & attack = 0 & a_hea > 0 & b_hea > 0 & c_hea <= 0 & d_hea > 0 ->
				1/2 : (attack' = 2) +
				1/2 : (attack' = 3) ;
	[team_1_turn] turn_clock = 1 & attack = 0 & a_hea > 0 & b_hea > 0 & c_hea > 0 & d_hea <= 0 ->
				1/2 : (attack' = 1) +
				1/2 : (attack' = 3) ;
	[team_1_turn] turn_clock = 1 & attack = 0 & a_hea <= 0 & b_hea > 0 & c_hea <= 0 & d_hea > 0 ->
				1 : (attack' = 3) ;
	[team_1_turn] turn_clock = 1 & attack = 0 & a_hea <= 0 & b_hea > 0 & c_hea > 0 & d_hea <= 0 ->
				1 : (attack' = 3) ;
	[team_1_turn] turn_clock = 1 & attack = 0 & a_hea > 0 & b_hea <= 0 & c_hea <= 0 & d_hea > 0 ->
				1 : (attack' = 2) ;
	[team_1_turn] turn_clock = 1 & attack = 0 & a_hea > 0 & b_hea <= 0 & c_hea > 0 & d_hea <= 0 ->
				1 : (attack' = 1) ;

	[team_2_turn] turn_clock = 2 & attack = 0 & c_hea > 0 & a_hea > 0 ->
				(attack' = 4) ;
	[team_2_turn] turn_clock = 2 & attack = 0 & c_hea > 0 & b_hea > 0 ->
				(attack' = 5) ;
	[team_2_turn] turn_clock = 2 & attack = 0 & d_hea > 0 & (a_hea > 0 | b_hea > 0) ->
				(attack' = 6) ;

	[A_C] attack = 1 & c_hea > 0 ->
			A_acc : (c_hea' = c_hea - A_dmg) & (attack' = 7) + (1-A_acc) : (attack' = 7);
	[A_D] attack = 2 & d_hea > 0 ->
			A_acc : (d_hea' = d_hea - A_dmg) & (attack' = 7) + (1-A_acc) : (attack' = 7);
	[B_opp] attack = 3 & c_hea > 0 & d_hea > 0 ->
			B_acc * B_acc : (c_hea' = c_hea - B_dmg) & (d_hea' = d_hea - B_dmg) & (attack' = 7) +
			B_acc * (1-B_acc) : (c_hea' = c_hea - B_dmg) & (attack' = 7) +
			B_acc * (1-B_acc) : (d_hea' = d_hea - B_dmg) & (attack' = 7) +
			(1-B_acc) * (1-B_acc) : (attack' = 7);
	[B_opp] attack = 3 & c_hea > 0 & d_hea <= 0 ->
			B_acc : (c_hea' = c_hea - B_dmg) & (attack' = 7) + (1-B_acc) : (attack' = 7);
	[B_opp] attack = 3 & c_hea <= 0 & d_hea > 0 ->
			B_acc : (d_hea' = d_hea - B_dmg) & (attack' = 7) + (1-B_acc) : (attack' = 7);
	[C_A] attack = 4 & a_hea > 0 ->
			C_acc : (a_hea' = a_hea - C_dmg) & (attack' = 7) + (1-C_acc) : (attack' = 7);
	[C_B] attack = 5 & b_hea > 0 ->
			C_acc : (b_hea' = b_hea - C_dmg) & (attack' = 7) + (1-C_acc) : (attack' = 7);
	[D_opp] attack = 6 & a_hea > 0 & b_hea > 0 ->
			D_acc * D_acc : (a_hea' = a_hea - D_dmg) & (b_hea' = b_hea - D_dmg) & (attack' = 7) +
			D_acc * (1-D_acc) : (a_hea' = a_hea - D_dmg) & (attack' = 7) +
			D_acc * (1-D_acc) : (b_hea' = b_hea - D_dmg) & (attack' = 7) +
			(1-D_acc) * (1-D_acc) : (attack' = 7);
	[D_opp] attack = 6 & a_hea > 0 & b_hea <= 0 ->
			D_acc : (a_hea' = a_hea - D_dmg) & (attack' = 7) + (1-D_acc) : (attack' = 7);
	[D_opp] attack = 6 & a_hea <= 0 & b_hea > 0 ->
			D_acc : (b_hea' = b_hea - D_dmg) & (attack' = 7) + (1-D_acc) : (attack' = 7);

	[next_turn] attack = 7 & turn_clock > 0 & (a_hea > 0 | b_hea > 0) & (c_hea > 0 | d_hea > 0) ->
			(turn_clock' = 3 - turn_clock) & (attack' = 0);

endmodule

init					//SINGLE INITIAL STATE
	a_hea = A_hea & b_hea = B_hea &
	c_hea = C_hea & d_hea = D_hea & 
	attack = 0 & turn_clock = 0
	& a_stun = false & b_stun = false & c_stun = false & d_stun = false
endinit

label "team_1_win" = (a_hea > 0 | b_hea > 0) & (c_hea <= 0 & d_hea <= 0);
label "team_2_win" = (c_hea > 0 | d_hea > 0) & (a_hea <= 0 & b_hea <= 0);
