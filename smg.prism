smg

// TEAM 1
	// Knight
const double A_acc = 0.75;
const int A_hea = 8;
const int A_dmg = 3;
	// Archer
const double B_acc = 0.75;
const int B_hea = 7;
const int B_dmg = 2;

// TEAM 2
	// Knight
const double C_acc = 0.75;
const int C_hea = 8;
const int C_dmg = 3;
	// Wizard
const double D_acc = 0.8;
const int D_hea = 7;
const int D_dmg = 2;

player p1
	[team_1_turn]
endplayer

player p2
	[team_2_turn]
endplayer

player sys
	[flip_coin], [A_C], [A_D], [B_opp], [C_A], [C_B], [D_A], [D_B], [next_turn]
endplayer


module game
	a_hea : [-2..A_hea];
	b_hea : [-2..B_hea];
	c_hea : [-2..C_hea];
	d_hea : [-2..D_hea];
	turn_clock : [0..2];
	attack : [0..8];			// Chosen action:
	// 0 : NONE, 1 : A_C, 2 : A_D, 3 : B_opp, 4 : C_A, 5 : C_B, 6 : D_A, 7 : D_B, 8 : NEXT_TURN.

	a_stun : bool;
	b_stun : bool;

	[flip_coin]	turn_clock = 0 ->
				0.5 : (turn_clock' = 1) + 0.5 : (turn_clock' = 2);

	[team_1_turn] turn_clock = 1 & attack = 0 & a_hea > 0 & a_stun = false & c_hea > 0 ->
				(attack' = 1) & (a_stun' = false) & (b_stun' = false) ;
	[team_1_turn] turn_clock = 1 & attack = 0 & a_hea > 0 & a_stun = false & d_hea > 0 ->
				(attack' = 2) & (a_stun' = false) & (b_stun' = false) ;
	[team_1_turn] turn_clock = 1 & attack = 0 & b_hea > 0 & b_stun = false & (c_hea > 0 | d_hea > 0) ->
				(attack' = 3) & (a_stun' = false) & (b_stun' = false) ;
	[team_1_turn] turn_clock = 1 & attack = 0 & (a_hea <= 0 | a_stun  = true) & (b_hea <= 0 | b_stun = true) ->
				(attack' = 8) & (a_stun' = false) & (b_stun' = false) ;

	[team_2_turn] turn_clock = 2 & attack = 0 & c_hea > 0 & a_hea > 0 ->
				(attack' = 4) ;
	[team_2_turn] turn_clock = 2 & attack = 0 & c_hea > 0 & b_hea > 0 ->
				(attack' = 5) ;
	[team_2_turn] turn_clock = 2 & attack = 0 & d_hea > 0 & a_hea > 0 ->
				(attack' = 6) ;
	[team_2_turn] turn_clock = 2 & attack = 0 & d_hea > 0 & b_hea > 0 ->
				(attack' = 7) ;

	[A_C] attack = 1 & c_hea > 0 ->
			A_acc : (c_hea' = c_hea - A_dmg) & (attack' = 8) + (1-A_acc) : (attack' = 8);
	[A_D] attack = 2 & d_hea > 0 ->
			A_acc : (d_hea' = d_hea - A_dmg) & (attack' = 8) + (1-A_acc) : (attack' = 8);
	[B_opp] attack = 3 & c_hea > 0 & d_hea > 0 ->
			B_acc * B_acc : (c_hea' = c_hea - B_dmg) & (d_hea' = d_hea - B_dmg) & (attack' = 8) +
			B_acc * (1-B_acc) : (c_hea' = c_hea - B_dmg) & (attack' = 8) +
			B_acc * (1-B_acc) : (d_hea' = d_hea - B_dmg) & (attack' = 8) +
			(1-B_acc) * (1-B_acc) : (attack' = 8);
	[B_opp] attack = 3 & c_hea > 0 & d_hea <= 0 ->
			B_acc : (c_hea' = c_hea - B_dmg) & (attack' = 8) + (1-B_acc) : (attack' = 8);
	[B_opp] attack = 3 & c_hea <= 0 & d_hea > 0 ->
			B_acc : (d_hea' = d_hea - B_dmg) & (attack' = 8) + (1-B_acc) : (attack' = 8);
	[C_A] attack = 4 & a_hea > 0 ->
			C_acc : (a_hea' = a_hea - C_dmg) & (attack' = 8) + (1-C_acc) : (attack' = 8);
	[C_B] attack = 5 & b_hea > 0 ->
			C_acc : (b_hea' = b_hea - C_dmg) & (attack' = 8) + (1-C_acc) : (attack' = 8);
	[D_A] attack = 6 & a_hea > 0 ->
			D_acc : (a_hea' = a_hea - D_dmg) & (a_stun' = true) & (attack' = 8) + 
			(1-D_acc) : (attack' = 8);
	[D_B] attack = 7 & b_hea > 0 ->
			D_acc : (b_hea' = b_hea - D_dmg) & (b_stun' = true) & (attack' = 8) + 
			(1-D_acc) : (attack' = 8);

	[next_turn] attack = 8 & turn_clock > 0 & (a_hea > 0 | b_hea > 0) & (c_hea > 0 | d_hea > 0) ->
			(turn_clock' = 3 - turn_clock) & (attack' = 0);

endmodule

init					//SINGLE INITIAL STATE
	a_hea = A_hea & b_hea = B_hea &
	c_hea = C_hea & d_hea = D_hea & 
	attack = 0 & turn_clock = 0
	& a_stun = false & b_stun = false
endinit

label "team_1_win" = (a_hea > 0 | b_hea > 0) & (c_hea <= 0 & d_hea <= 0);
label "team_2_win" = (c_hea > 0 | d_hea > 0) & (a_hea <= 0 & b_hea <= 0);
